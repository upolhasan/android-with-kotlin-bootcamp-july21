plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-parcelize'
  id "com.google.protobuf" version "0.8.12"
  id "org.jetbrains.kotlin.kapt"
}

android {
  compileSdkVersion 30
  buildToolsVersion "30.0.3"

  defaultConfig {
    applicationId "dev.goobar.androidstudyguidejuly21"
    minSdkVersion 23
    targetSdkVersion 30
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // add your GitHub token to ~/.gradle/gradle.properties to access without checking in to repo
    buildConfigField "String", "GITHUB_ANDROID_WORKSHOP_TOKEN", GITHUB_ANDROID_WORKSHOP_TOKEN
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

dependencies {

  implementation 'androidx.core:core-ktx:1.6.0'
  implementation 'androidx.appcompat:appcompat:1.3.0'
  implementation 'com.google.android.material:material:1.4.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
  implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

  implementation "androidx.datastore:datastore:1.0.0-rc01"
  implementation  "com.google.protobuf:protobuf-javalite:3.11.0"

  // Room dependencies
  def room_version = "2.3.0"

  implementation("androidx.room:room-runtime:$room_version")
  kapt("androidx.room:room-compiler:$room_version")
  implementation("androidx.room:room-ktx:$room_version")

  // Volley will help us make network requests
  implementation("com.android.volley:volley:1.2.0")

  // Moshi is a serialization library that will help us deserialize the Json response returned
  // from the network requests we make with Volley
  implementation("com.squareup.moshi:moshi:1.12.0")
  kapt("com.squareup.moshi:moshi-kotlin-codegen:1.12.0")

  // WorkManager
  implementation "androidx.work:work-runtime-ktx:2.5.0"

  // Access Google Play Services' FusedLocationProvider
  implementation 'com.google.android.gms:play-services-location:18.0.0'

  testImplementation 'junit:junit:4.+'
  testImplementation "androidx.arch.core:core-testing:2.1.0"
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'

  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test:runner:1.4.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

/**
 * Sets up protobuf code generation so we can generate DataStore models from our protobuf messages
 */
protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.11.0"
  }

  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
    }
  }
}